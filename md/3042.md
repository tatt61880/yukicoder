Kuinでコードゴルフしました。

Kuinエディタは自動で整形が行われるため、提出前に別のエディタで余分な空白類文字を削除しましょう。

----
普通に書くとこんな感じです。
```kuin
func main()
	var N: int :: cui@input().toInt(&)
	var lines: [][]char :: #[N][]char
	
	for i(0, ^lines - 1)
		do lines[i] :: cui@input()
	end for
	
	do lines.sort()
	
	for i(0, ^lines - 1)
		do cui@print(lines[i] ~ "\n")
	end for
end func
```

----
無駄な空白類文字を削除し、変数名を短くするとこんな感じ。
```kuin
func main()
var a:[][]char::#[cui@input().toInt(&)][]char
for i(0,^a-1)
do a[i]::cui@input()
end for
do a.sort()
for i(0,^a-1)
do cui@print(a[i]~"\n")
end for
end func
```
これで167 Byte。

----
組み込みメソッド `join` を用いることで 出力時の for ループをなくせます。
```kuin
func main()
var a:[][]char::#[cui@input().toInt(&)][]char
for i(0,^a-1)
do a[i]::cui@input()
end for
do a.sort()
do cui@print(a.join("\n"))
end func
```
これで148 Byte。

----
for 文内で `[i]` が不要になるように工夫したところ、6バイト削れました。
```kuin
func main()
var a:[][]char::#[0,0]char
for(1,cui@input().toInt(&))
do a:~[cui@input()]
end for
do a.sort()
do cui@print(a.join("\n"))
end func
```
142 Byte。これが限界かと思いました。

----
`.toInt(&)` を削るように工夫したら、結果的に2バイト削れました。  
入力行数を上回って `cui@input()` すると、その値は `""` になるため、 `a` に `[""]` が追加されていきます。  
ソート後の `a` に対し `a.join("\n")` として生成される文字列は前半に改行が沢山並んでいますが、 `trim` でその改行を削れます。
```kuin
func main()
var a:[][]char::[""]
do cui@input()
for(1,50)
do a:~[cui@input()]
end for
do a.sort()
do cui@print(a.join("\n").trim())
end func
```
これで 140 Byte。

----
1年ほど経ってから140バイトのコードを見直すと、「 `cui@input` が2箇所にあるのをどうにか削れないかな？」と思いました。
```kuin
func main()
var a:[][]char::[""]
for i(1,51)
do a:~[a~a,[cui@input()]][^a-i]
end for
do a.sort()
do cui@print(a.join("\n").trim())
end func
```
削れました。これで139 Byte。

----
この解説記事を更新している最中に、以前 `cui@input().toInt(&)` と書いていた箇所が、現在のKuinでは `cui@inputInt()` と書けることに気付きました。
```kuin
func main()
var a:[][]char::#[0,0]char
for(1,cui@inputInt())
do a:~[cui@input()]
end for
do a.sort()
do cui@print(a.join("\n"))
end func
```
これで136 Byte。

----
「`cui@delimiter()` を用いて `inputStr()` における改行の扱いを変更し、更に `split` 	メソッドを使うことでfor文を減らせる」というアイデアを思いつきました。
```kuin
func main()
do cui@delimiter(cui@input())
var a:[][]char::cui@inputStr().split("\n")
do a.sort()
do cui@print(a.join("\n"))
end func
```
これは132 Byteなのですが、残念ながらWAです。
入力の最後にも「\n」があるため、空の文字列が `a` に含まれます。 `sort` によって最初に空行が含まれます。
`.trim()` を追加すると 7バイト増えて139 Byteになってしまいます。

----
上記の問題を解決する方法がこちらです。  
`a.sort()` の後、 `a.sub(2,^a-2)` により、入力1行目と空行をまとめて削ります。
```kuin
func main()
do cui@delimiter("")
var a:[][]char::cui@inputStr().split("\n")
do a.sort()
do cui@print(a.sub(2,^a-2).join("\n"))
end func
```
これで135 Byte。

----
`sub` メソッドの第二引数を `-1` にすることで、配列の最後まで切り出せます。
```kuin
func main()
do cui@delimiter("")
var a:[][]char::cui@inputStr().split("\n")
do a.sort()
do cui@print(a.sub(2,-1).join("\n"))
end func
```
これで133 Byte。

----

これを下回るようなコードを思いついたら教えてください。