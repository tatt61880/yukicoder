; userIdのみ要変更です。
const userId: int :: 5112
const yukicoderBase: []char :: "https://yukicoder.me/"
const dirHtml: []char :: "./html/"
const dirSubmissions: []char :: "./submissions/"
var dictSubmitTitle: dict<int, []char>
var dictSubmitLang: dict<int, []char>
var dictSubmitTime: dict<int, []char>
var dictSubmitByte: dict<int, []char>
var dictLangExtension: dict<[]char, []char>
var regexUrl: regex@Regex
var regexUrl2: regex@Regex
var maxId: int

func main()
	if(!@makeDir(@dirHtml))
		do @pause()
		do lib@exitCode(1)
		ret
	end if
	
	{
    <td><a href="/submissions/322195">322195</a></td>
    <td class="time">2019-03-12 00:09:26</td>
    <td><a href="?submitter=5112" title="このユーザーの提出の表示"><i class="fa fa-search"></i></a></td>
    <td class="table_username">
        <a href="/users/5112"><img  alt="tatt61880"  class="icon" src="https://avatars0.githubusercontent.com/u/2398152?v=4">tatt61880</a></td>
    <td><a href="/problems/no/129">No.129 お年玉(2)</a></td>
    <td>Kuin(beta)</td>
	}
	do @regexUrl :: regex@makeRegex("\"/submissions/(\\d+)\"[\\d\\D]*?/problems/no/(\\d+)\">(.*?)</a></td>\\s*<td>(.*?)</td>\\s*<td>(?:.*?)</td>\\s*<td>(.*?)</td>\\s*<td>(.*?)</td>")
	assert @regexUrl <>& null
	do @regexUrl2 :: regex@makeRegex("/submissions/(\\d+)/")
	assert @regexUrl2 <>& null
	
	var id: int :: 0
	var dictProblemSubmit: dict<int, int> :: #dict<int, int>
	do @dictSubmitTitle :: #dict<int, []char>
	do @dictSubmitLang :: #dict<int, []char>
	do @dictSubmitTime :: #dict<int, []char>
	do @dictSubmitByte :: #dict<int, []char>
	while loop(true)
		do id :+ 1
		var url: []char :: "\{@yukicoderBase}users/\{@userId}/submissions?page=\{id}&status=AC&date_asc=enabled"
		var filepath: []char :: "\{@dirHtml}\{id}.html"
		var fetchFlag: bool :: true
		if(file@exist(filepath))
			var filepathNext: []char :: "\{@dirHtml}\{id + 1}.html"
			if(file@exist(filepathNext))
				; データが更新されていないと思われるページは取得しません。
				do fetchFlag :: false
			end if
		end if
		var data: []char
		if(fetchFlag)
			do data :: @fetchData(url)
			if(data =& null)
				break loop
			end if
		else
			var reader: file@Reader :: file@makeReader(filepath)
			do reader.delimiter("")
			do data :: reader.readStr()
		end if
		
		var result: [][][]char :: @regexUrl.findAll(&, data)
		if(result =& null)
			break loop
		end if
		if(fetchFlag)
			var writer: file@Writer :: file@makeWriter(filepath, false)
			do writer.writeStr(data)
			do writer.fin()
		end if
		
		for i(0, ^result - 1)
			var subumitId: int :: result[i][1].toInt(&)
			var problemId: int :: result[i][2].toInt(&)
			do @maxId :: lib@max(@maxId, problemId)
			var title: []char :: result[i][3]
			var language: []char :: result[i][4]
			var time: []char :: result[i][5].trim()
			var bytes: []char :: result[i][6].trim().replace("<br />", " ")
			
			do title :: @unsanitized(title)
			do language :: @unsanitized(language)
			
			if(problemId = 9004 | problemId = 9007)
				skip i
			end if
			do dictProblemSubmit.add(problemId, subumitId)
			do @dictSubmitTitle.add(subumitId, title)
			do @dictSubmitLang.add(subumitId, language)
			do @dictSubmitTime.add(problemId, time)
			do @dictSubmitByte.add(problemId, bytes)
		end for
	end while
	
	if(!@makeDir(@dirSubmissions))
		do @pause()
		do lib@exitCode(1)
		ret
	end if
	
	if(!@initExtensions())
		do cui@print("Error: 拡張子データの初期化に失敗しました。\n")
		do @pause()
		do lib@exitCode(1)
		ret
	end if
	
	do dictProblemSubmit.forEach(callback, null)
	
	block
		var writer: file@Writer :: file@makeWriter("submissions/times.txt", false)
		for subumitId(1, @maxId)
			var time: []char :: @dictSubmitTime.get(subumitId, &)
			if(time =& null)
				do writer.writeStr("\n")
			else
				do writer.writeStr("\{time}\n")
			end if
		end for
		do writer.fin()
	end block
	block
		var writer: file@Writer :: file@makeWriter("submissions/bytes.txt", false)
		for subumitId(1, @maxId)
			var byte: []char :: @dictSubmitByte.get(subumitId, &)
			if(byte =& null)
				do writer.writeStr("\n")
			else
				do writer.writeStr("\{byte}\n")
			end if
		end for
		do writer.fin()
	end block
	
	func callback(problemId: int, subumitId: int, _: kuin@Class): bool
		var title: []char :: @dictSubmitTitle.get(subumitId, &)
		var language: []char :: @dictSubmitLang.get(subumitId, &)
		var extension: []char :: @dictLangExtension.get(language, &)
		if(extension =& null)
			do @addExtension(language, &extension)
		end if
		var filepath: []char :: "\{@dirSubmissions}\{problemId}/main.\{extension}"
		var filepathUrl: []char :: "\{@dirSubmissions}\{problemId}/submission.url"
		var filepathTitle: []char :: "\{@dirSubmissions}\{problemId}/title.txt"
		var url: []char :: "\{@yukicoderBase}submissions/\{subumitId}/"
		if(file@exist(filepathUrl))
			var readerUrl: file@Reader :: file@makeReader(filepathUrl)
			do readerUrl.readLine()
			var ss: [][]char :: readerUrl.readLine().split("=")
			if(^ss = 2)
				var currentUrl: []char :: ss[1]
				do readerUrl.fin()
				var result: [][][]char :: @regexUrl2.findAll(&, currentUrl)
				if(result <>& null)
					var currentSubumitId: int :: result[0][1].toInt(&)
					if(subumitId <= currentSubumitId)
						ret true
					end if
				end if
			end if
		end if
		var urlSource: []char :: "\{@yukicoderBase}submissions/\{subumitId}/source"
		do cui@print("No.\{problemId}: ")
		var data: []char :: @fetchData(urlSource)
		if(data =& null)
			do cui@print("Error: Failed to find source code. [\{urlSource}]\n")
			do lib@exitCode(1)
			do @pause()
			while(true)
				do cui@print("処理を続行しますか？ (y/n): ")
				var s: []char :: cui@input()
				switch(s)
				case "y"
					ret true
				case "n"
					ret false
				end switch
			end while
		end if
		
		var dirPath: []char :: "\{@dirSubmissions}\{problemId}/"
		for i(0, 10)
			if(file@exist(dirPath))
				if(!file@delDir(dirPath))
					if(i = 10)
						do cui@print("Error: Failed to remove dir. [\{dirPath}]\n")
						do @pause()
						do lib@exitCode(1)
						ret false
					end if
					do lib@sleep(200)
				else
					do lib@sleep(500)
				end if
			else
				break i
			end if
		end for
		if(!@makeDir(dirPath))
			do @pause()
			do lib@exitCode(1)
			ret false
		end if
		var writer: file@Writer :: file@makeWriter(filepath, false)
		do writer.writeStr(data)
		do writer.fin()
		
		var writerUrl: file@Writer :: file@makeWriter(filepathUrl, false)
		do writerUrl.writeStr("[InternetShortcut]\n")
		do writerUrl.writeStr("URL=" ~ url)
		do writerUrl.fin()
		
		var writerTitle: file@Writer :: file@makeWriter(filepathTitle, false)
		do writerTitle.writeStr(title)
		do writerTitle.fin()
		ret true
	end func
end func

func fetchData(url: []char): []char
	if(url =& null)
		do cui@print("Error: urlがnullです。\n")
		ret null
	end if
	var http: net@Http :: net@makeHttp(url, false, "Kuin")
	do cui@print("Fetching data: \{url}\n")
	do lib@sleep(1500)
	
	if(http =& null)
		do cui@print("Error: 接続に失敗しました。[\{url}]\n")
		do @pause()
		ret null
	else
		var data: []char
		for i(1, 300)
			do lib@sleep(10)
			do data :: http.get()
			if(data <>& null)
				ret data
			end if
		end for
		do http.fin()
	end if
	do cui@print("Error: データの取得に失敗しました。[\{url}]\n")
	do @pause()
	ret null
end func

func makeDir(dirPath: []char): bool
	if(file@exist(dirPath))
		ret true
	else
		if(file@makeDir(dirPath))
			ret true
		else
			do cui@print("Error: Failed to create dir. [\{dirPath}]\n")
			ret false
		end if
	end if
end func

var arrLanguages: [][]char
var arrExtensions: [][]char

func initExtensions(): bool
	var extensionFilepath: []char :: "\{wnd@exeDir()}extensions.csv"
	var reader: file@Reader :: file@makeReader(extensionFilepath)
	if(reader =& null)
		do cui@print("Error: \{extensionFilepath} can't be opened.\n")
		ret false
	end if
	do reader.delimiter(",\n")
	do @arrLanguages :: #[0][]char
	do @arrExtensions :: #[0][]char
	while(!reader.term())
		var language: []char :: reader.readStr()
		var extension: []char :: reader.readStr()
		do @arrLanguages :~ [language]
		do @arrExtensions :~ [extension]
	end while
	do @dictLangExtension :: #dict<[]char, []char>
	ret true
end func

func addExtension(language: []char, extension: &[]char)
	do extension :: "unknown"
	for i(^@arrLanguages - 1, 0, -1)
		if(language.findStr(@arrLanguages[i], -1) <> -1)
			do extension :: @arrExtensions[i]
			break i
		end if
	end for
	do @dictLangExtension.add(language, extension)
end func

func unsanitized(str: []char): []char
	var res: []char :: ""
	var i: int :: 0
	while(i < ^str)
		if(i < ^str - 1 & str[i] = '&')
			if(str[i + 1] = '#')
				do i :+ 2
				var v: int :: 0
				while(i < ^str & str[i] <> ';')
					do v :* 10
					do v :+ str[i] $ int - '0' $ int
					do i :+ 1
				end while
				do res :~ [v $ char]
			else
				do res :~ ['&']
			end if
		else
			do res :~ [str[i]]
		end if
		do i :+ 1
	end while
	ret res
end func

func pause()
	do cui@print("Press Enter-key to continue...\n")
	do cui@input()
end func
